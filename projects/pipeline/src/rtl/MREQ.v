`timescale 1ns / 1ps

`include "defines.vh"

module MREQ (
    input  wire [31:0]  ram_addr,

    input  wire [ 2:0]  ram_rop,
    output reg  [ 3:0]  da_ren,
    output wire [31:0]  da_addr,

    input  wire [ 3:0]  ram_wop,
    input  wire [31:0]  ram_wdata,
    output reg  [ 3:0]  da_wen,
    output reg  [31:0]  da_wdata
);

    wire [1:0] offset = ram_addr[1:0];
    wire [4:0] data_shift = {offset, 3'b000};

    assign da_addr = ram_addr;

    // 产生写访存请求（da_wen、da_wdata）
    always @(*) begin
        // default value
        da_wen   = 4'h0;
        da_wdata = ram_wdata;

        case (ram_wop)
            `RAM_WE_B: begin
                da_wen   = 4'h1 << offset;
                da_wdata = ram_wdata << data_shift;
            end
            `RAM_WE_H: begin
                if (offset[0] == 1'b0) begin
                    da_wen   = 4'h3 << offset;
                    da_wdata = ram_wdata << data_shift;
                end
            end
            `RAM_WE_W:
                if (offset == 2'h0) begin
                    da_wen   = 4'hF;
                end
        endcase
    end

    // 产生读访存请求（da_ren）
    always @(*) begin
        if (ram_rop != `RAM_EXT_N) begin
            case (ram_rop)
                `RAM_EXT_B, `RAM_EXT_BU: da_ren = 4'hF;
                `RAM_EXT_H, `RAM_EXT_HU: da_ren = (offset[0] == 1'b0) ? 4'hF : 4'h0;
                `RAM_EXT_W:              da_ren = (offset == 2'h0) ? 4'hF : 4'h0;
                default:                 da_ren = 4'h0;
            endcase
        end else
            da_ren = 4'h0;
    end

endmodule
